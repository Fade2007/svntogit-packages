# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kross-interpreters
pkgver=21.03.80
pkgrel=1
pkgdesc='Language interpreters to enable in-process scripting with Kross'
arch=(x86_64)
url='https://www.kde.org/applications/development/'
license=(GPL)
depends=(kross)
makedepends=(extra-cmake-modules kdoctools python2 ruby)
optdepends=('python2: kross-python plugin' 'ruby: kross ruby plugin')
groups=(kde-applications)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
       kross-interpreters-20.08.3-Drop-safe-level-support-in-more-recent-Rubies.patch)
sha256sums=('3f2fb9fd3ac9939468e9f1d4277ff272b1225b4c157f01642d5cf8ea1eede312'
            'SKIP'
            '0d5532869ef281c2efc1b9e0840d14edd408a086af2ebad39c10900108b9e812')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  cd $pkgname-$pkgver
  patch -p1 < ../kross-interpreters-20.08.3-Drop-safe-level-support-in-more-recent-Rubies.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
